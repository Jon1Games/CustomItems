plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'de.jonas'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT"
    implementation("de.tr7zw:item-nbt-api:2.12.2")
    implementation('com.zaxxer:HikariCP:5.1.0')
    implementation "dev.jorel:commandapi-bukkit-shade:9.2.0"
    implementation('org.mariadb.jdbc:mariadb-java-client:3.3.2') {
        exclude group: 'com.github.waffle', module: 'waffle-jna'
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set('customitems')
    archiveClassifier.set('')
    relocate("dev.jorel.commandapi", "de.jonas.customitems.commandapi")
    relocate("de.tr7zw.changeme.nbtapi", "org.gaminglounge.skyblock.nbtapi")
}
